apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 3
        versionName "1.0.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
}

bintray {
    user = System.getenv('bintrayUser')
    key = System.getenv('bintrayKey')
    publications = ['ripplebackground']
    pkg {
        repo = 'ripplebackground'
        name = 'ripplebackground'
        websiteUrl = 'https://github.com/jitinsharma/android-ripple-background'
        vcsUrl = 'https://github.com/jitinsharma/android-ripple-background.git'
        licenses = ["MIT"]
        publish = true
        version {
            name = android.defaultConfig.versionName
            desc = 'Ripple Background'
            released  = new Date()
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        ripplebackground(MavenPublication) {
            groupId 'io.github.jitinsharma'
            artifactId 'ripplebackground'
            version android.defaultConfig.versionName
            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}
